buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // https://github.com/google/protobuf-gradle-plugin
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id "com.google.protobuf" version "0.8.1"
}

group 'me.nicholasnadeau'

if (project.hasProperty('tag')) {
    version = project.tag
} else {
    version = '0'
}

sourceCompatibility = 1.6

protobuf {
    protoc {
        // https://mvnrepository.com/artifact/com.google.protobuf/protoc
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }

    generateProtoTasks {
        // all() returns the collection of all protoc tasks
        all().each { task ->
            task.builtins {
                java {}
                cpp {}
            }
        }
    }

    // https://github.com/google/protobuf-gradle-plugin#intellij-idea
    generatedFilesBaseDir = "$projectDir/gen"
}

// https://github.com/google/protobuf-gradle-plugin#intellij-idea
clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.2.0'
}

shadowJar.archiveName = "$project.name-$project.version-all.jar"

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}